spice
* p type inv amp w/bti
.options TEMP=25
.options ttsteporder=3

.option nopicky
.option nocstray

.load public models-uf.so
.load rcd_sym.so
.options ttstepgrow=2

.options reltol=1e-5

.param pulse_iv=0
.param pulse_rise=1n

.param voff=0.4
.model my_rcdmodel rcd_exp

.model bti_ptm_approx bti_sum (
+ parms = (
+(1.28936788433 ,  -0.0, 13.433303380408852, 43.576925888911987, 1.4509831704140836),
+(0.0425895446518 ,-0.0, 4.1230027350333067, 44.168622555330224, 0.35639368027120355),
+(0.00986204739979,-0.046730726782108274, 6.9322844619511379, 34.467824008855281, 19.76034507753667),
+(323556.834838,   -0.74091304338940822, 7.9999074744048002, 50.0, 25.0),
+(65734.7534834,   -0.0, 7.6851098109742111, 33.944029750879885, 20.802215970322095),
+(25.5541996901,   -46.321386556453803, 16.844440862184946, 11.976323270474861, 23.296371309229073)
+)
+rcd_model_name=my_rcdmodel weight=1)

.model cmosp pmos(
+level=8
+bti_model=bti_ptm_approx
+ ALPHA0 = 6.0E-08 BETA0 = 12
+ H0 = 0
+)

.param vdd=5
.param x=0
V1 ndd nin {1.9+x} ic=1.9
MP1 nctrl ng ndd ndd cmosp l=3.5e-5 w=1e-4
R1 0 nctrl 1000k

.ttcap c1 nctrl 0 400n ic=2.0

VDD nddext 0 vdd ic=vdd
sw1 ndd nddext nctrl 0 pd
sw2 nin ng nctrl 0 pd

roff1 ndd 0 1k
roff2 ng 0 1k

.model pd sw vt=1.5 vh=1u ron=1e-12 roff=1e12

.print dc v(nodes) s0(sw1) s1(sw1) vin(sw1) ic(sw1) converged(sw1) v(c1)
.dc uic

.print tran v(nodes) s0(sw1) r(sw1) v(c1) hidden(0) ttstate(c1) charge(c1) dvth(MP1) last(sw1)

.store tw dvth(mp1.bti)

.print tw
+ hidden(0)
+ ttgain(0)
+ event(sw1)
+ in(sw1)
+ v(c1)
+ pred(c1)
+ corr(c1)
+ preddt(c1)
+ ttdelta(c1)
+ s0(sw1)
+ ttord(c1)
+ dv(c1)
+ chisq(c1)
+ ttstep0(c1)
+ ttstep1(c1)
+ ttstep2(c1)
+ udc(mp1.bti.rcd3)
+ dvth(mp1.bti)

*+ meas({max(probe="v(ndd)")})
*+ meas({min(probe="v(ndd)")})

.alarm tw v(c1)(-1e-3,2.5)
.printf "", > sd_tt.out
.printf "", > sd_tr.out
.echo #x tsw1_5 tsw2 dvth1_5 dvth2 > sdtsw.out echo

.sweep 3 x -1e-2 1e-2
.dc uic > /dev/null
.store tw dvth(mp1.bti)
.tw 10u .1m 100 * 1.5 new alarm=abort trace=a basic >> sd_tt.out tran cont trace=n >> sd_tr.out
.measure tsw=max(probe="dvth(mp1.bti)" arg) > /dev/null
.measure dvth=max(probe="dvth(mp1.bti)") > /dev/null

.echo >> sd_tt.out

.tw 0 new > /dev/null tran > /dev/null
.dc uic > /dev/null
.store tw dvth(mp1.bti)
.tw 10u .1m 100 * 2 new alarm=abort trace=a basic >> sd_tt.out tran cont trace=n >> sd_tr.out
.measure tsw2=max(probe="dvth(mp1.bti)" arg) > /dev/null
.measure dvth2=max(probe="dvth(mp1.bti)") > /dev/null

.echo >> sd_tt.out

.tw 0 new > /dev/null tran > /dev/null

.printf "%f %f %f %f %f\n", x, tsw, tsw2, dvth, dvth2, >> sdtsw.out echo
go

.status notime
.end
